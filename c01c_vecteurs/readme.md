# Les vecteurs <!-- omit in toc -->

[Liste de vid√©os de support](https://www.youtube.com/watch?v=mWJkvxQXIa8&list=PLRqwX-V7Uu6ZwSmtE13iJBcoI-r4y7iEc)

# Table des mati√®res <!-- omit in toc -->
- [Vecteur : d√©finition](#vecteur--d√©finition)
- [Vecteur : utilit√©](#vecteur--utilit√©)
- [Vecteur : exemple](#vecteur--exemple)
  - [Simplification de l'exemple avec les vecteurs](#simplification-de-lexemple-avec-les-vecteurs)
- [Vecteur : classe](#vecteur--classe)
- [Vecteur : d√©placement](#vecteur--d√©placement)
- [Exercice 1 : Balle qui rebondit avec vecteurs](#exercice-1--balle-qui-rebondit-avec-vecteurs)
- [Op√©rations d‚Äôint√©r√™t](#op√©rations-dint√©r√™t)
- [Termes √† conna√Ætre](#termes-√†-conna√Ætre)
- [Acc√©l√©ration](#acc√©l√©ration)
- [Autres op√©rations](#autres-op√©rations)
- [Trajectoire](#trajectoire)
- [Exercice](#exercice)
- [Concepts avanc√©s (Bonus)](#concepts-avanc√©s-bonus)
  - [Interpolation lin√©aire (lerp)](#interpolation-lin√©aire-lerp)
  - [Distance entre deux points](#distance-entre-deux-points)
  - [Rotation d'un vecteur](#rotation-dun-vecteur)
  - [Angle entre deux vecteurs](#angle-entre-deux-vecteurs)
  - [Applications pratiques](#applications-pratiques)
- [R√©f√©rences](#r√©f√©rences)
  - [Ressources suppl√©mentaires](#ressources-suppl√©mentaires)



# Vecteur : d√©finition
- Le terme **vecteur** peut signifier plusieurs choses d√©pendant du contexte.
  - En biologie : D√©crit un organisme qui transmet une infection d‚Äôun h√¥te √† un autre.
  - En programmation : D√©crit une structure de tableau de donn√©es.
- En math√©matique, un vecteur est un concept permettant de repr√©senter une longueur (magnitude) et une direction.

![alt text](assets/Image1.png)

# Vecteur : utilit√©
- Dans le monde des jeux vid√©o, r√©alit√© virtuelle ou autre simulation, les vecteurs sont utilis√©s partout.
- **C‚Äôest une connaissance fondamentale √† la programmation de jeux et applis multim√©dia.**
- C‚Äôest un bloc de construction n√©cessaire pour toute application ayant des implications math√©matiques.

---

# Vecteur : exemple

- Voici du code repr√©sentant une balle qui rebondit aux limites de l‚Äô√©cran
- [Lien pour l‚Äôex√©cuter](pde://github.com/nbourre/0sw_processing_exemples/raw/master/bin/s01_no_vectors.pdez)
  - Au moment d‚Äô√©crire ces lignes, il y avait un bug dans Processing. Il faut cliquer une 2e fois sur le lien tout en ayant une fen√™tre ouverte.

![alt text](assets/Image2.png)

- Ce que l‚Äôon remarque est l‚Äôutilisation de plusieurs variables X et Y similaires.
  - Position X et Y.
  - Vitesse X et Y.
- Une des complications est la gestion de toutes ces variables.
- Imaginez maintenant que vous devez g√©rer l‚Äôacc√©l√©ration, la position d‚Äôune cible, le vent et la friction.
  - Quelles seraient les variables probables?
  - Utilisation de deux variables dans chacun des cas.
  - Dans un monde 3D ce serait 3 variables‚Ä¶

---

## Simplification de l'exemple avec les vecteurs

<table style="border: none;">

<tr>
<td>

```java
float x;
float y;
float z;
```

</td>
<td>

Vecteur location; // ou position

</td>
</tr>

<tr>
<td>

```java
float xSpeed;
float ySpeed;
float zSpeed;
```

</td>
<td>

Vecteur speed; // ou velocity

</td>
</tr>

<tr>

<td  colspan="2">

On simplifie le code en utilisant les vecteurs.

</td>
</tr>

</table>


---

# Vecteur : classe
- Processing offre la classe `PVector` qui repr√©sente un vecteur.
- Dans cette classe, on y retrouve les propri√©t√©s X et Y en `float`.
- On y retrouve plusieurs m√©thodes pour effectuer des op√©rations avec les vecteurs.

**Cr√©ation d'un vecteur** :
```java
// Cr√©ation d'un vecteur √† la position (10, 20)
PVector position = new PVector(10, 20);

// Cr√©ation d'un vecteur de vitesse
PVector vitesse = new PVector(2, -1); // 2 pixels/frame vers la droite, 1 pixel/frame vers le haut

// Acc√®s aux composantes
float x = position.x;  // R√©cup√®re la composante X
float y = position.y;  // R√©cup√®re la composante Y
```

**Propri√©t√©s principales** :
- `x` : Composante horizontale
- `y` : Composante verticale (attention : Y augmente vers le bas dans Processing)
- `z` : Composante en profondeur (pour la 3D)

---

# Vecteur : d√©placement
- Pour simuler du mouvement √† l‚Äôaide des vecteurs, il faut utiliser la translation.
  - Pour effectuer une translation, il suffit d‚Äôadditionner la vitesse √† la position.
- Le mouvement est un d√©placement dans le temps.
- La vitesse repr√©sente un d√©placement dans le temps.
- La vitesse peut √™tre repr√©sent√©e par un vecteur.
- Le d√©placement est une distance dans une unit√© donn√©e.
  - Exemple : L‚Äôunit√© pixel.

```java
// Variables globales
PVector location;
PVector vitesse;

void setup() {
  size(400, 300);
  location = new PVector(50, 50);    // Position initiale
  vitesse = new PVector(2, 1.5);     // 2 pixels/frame en X, 1.5 en Y
}

void draw() {
  background(255);
  
  // D√©placement : position = position + vitesse
  location.add(vitesse);
  
  // Dessiner la balle
  ellipse(location.x, location.y, 20, 20);
  
  // Rebond sur les bords
  if (location.x > width || location.x < 0) {
    vitesse.x *= -1;  // Inverse la direction X
  }
  if (location.y > height || location.y < 0) {
    vitesse.y *= -1;  // Inverse la direction Y
  }
}
```

**√âquivalent sans vecteur** (plus verbeux) :
```java
float locX = 50, locY = 50;
float vitX = 2, vitY = 1.5;

// Dans draw()
locX += vitX;
locY += vitY;
// + gestion des rebonds pour chaque composante...
```

---

# Exercice 1 : Balle qui rebondit avec vecteurs
**Objectif** : Convertir une animation de balle qui rebondit pour utiliser des vecteurs.

**Instructions d√©taill√©es** :
1. Cr√©ez deux variables globales de type `PVector` :
   - `position` : pour la position de la balle
   - `vitesse` : pour la vitesse de d√©placement

2. Dans `setup()` :
   - Initialisez `position` au centre de l'√©cran
   - Initialisez `vitesse` avec des valeurs comme (3, 2)

3. Dans `draw()` :
   - Effacez l'√©cran avec `background()`
   - D√©placez la balle : `position.add(vitesse)`
   - Dessinez la balle √† la position actuelle
   - G√©rez les rebonds en inversant les composantes de vitesse

**Code de base √† compl√©ter** :
```java
PVector position;
PVector vitesse;

void setup() {
  size(600, 400);
  // TODO: Initialiser position et vitesse
}

void draw() {
  background(240);
  
  // TODO: D√©placer la balle
  // TODO: Dessiner la balle
  // TODO: G√©rer les rebonds
}
```

**Bonus** : Ajoutez des couleurs ou faites varier la taille de la balle.

---

# Op√©rations d‚Äôint√©r√™t
- Soustraction
  - Idem que l‚Äôaddition.
  - M√©thode `sub(PVector)`.
  - Exemple : Pour trouver la distance entre deux vecteurs.
- Multiplication par un scalaire
  - On multiplie chacun des composants du vecteur par une valeur scalaire.
  - ùê¥ ‚àó ùë£ (ùë•, ùë¶) = ùë£ (ùê¥ùë•, ùê¥ùë¶).
  - M√©thode `mult(float)`.
- Division par un scalaire
  - Idem que la multiplication.

---

# Termes √† conna√Ætre

- Magnitude
  - La magnitude est la longueur du vecteur en utilisant le th√©or√®me de Pythagore.
- Normalisation
  - Ram√®ne le vecteur √† une longueur de 1 unit√©.
  - On divise le vecteur par sa longueur.
  - Cela donne la direction du vecteur.
  - Exemple : Pour limiter la vitesse d‚Äôun objet. On normalise le vecteur de vitesse puis on le multiplie par la vitesse maximale.

# Acc√©l√©ration
- L‚Äôacc√©l√©ration est le taux de variation de la vitesse.
- En programmation, on additionne l‚Äôacc√©l√©ration √† la vitesse.

```java
acceleration = new PVector(1, 1);
vitesse = new PVector(0, 0);
vitesse.add(acceleration);
location.add(vitesse);
```

---

# Autres op√©rations
- Dans un jeu, on limite souvent les vitesses.
- Pour limiter les vitesses, on ajoute une m√©thode qui limite la longueur d‚Äôun vecteur.
- L‚Äôalgorithme est le suivant :
  - Si `vecteur.longueur > max`
    - `vecteur.normalise()` // On le met √† une longueur de 1 unit√©.
    - `vecteur.mult(max)`
  - Fin si

---

# Trajectoire
- √Ä chaque fois que l‚Äôon d√©sire calculer une trajectoire, il faut calculer la magnitude et la direction.
- Prenons l‚Äôexemple o√π l‚Äôon d√©sire que notre objet se d√©place vers la souris.
- Calculons la direction.
  - Celle-ci est la distance entre le X et Y de l‚Äôobjet et le X et Y de la souris.

```java
PVector souris = new PVector(mouseX, mouseY);
PVector dir = PVector.sub(souris, location);
```

![alt text](assets/Image3.png)

---

- Nous avons maintenant le vecteur qui pointe directement √† l‚Äôemplacement de la souris.
- Si nous additionnons la direction √† la position, l'objet appara√Ætrait imm√©diatement √† la souris et ce n'est pas l'effet d√©sir√©.
- Ce que l'on doit faire, c'est de d√©cider √† quelle vitesse l'objet doit se rendre √† la souris.
- Pour ce faire, on normalisera le vecteur pour ensuite le multiplier par une valeur qui d√©terminera sa vitesse en unit√©.
- Pour finaliser, on applique ce vecteur √† l'acc√©l√©ration.

---

# Exercice
- Modifiez l‚Äôexercice avec la balle pour avoir une forme qui acc√©l√®re dans la direction de la fl√®che appuy√©e par l‚Äôutilisateur.
- Faites un projet o√π une image (cible, balle, etc.) poursuit la souris.
  - Essayez avec diff√©rentes vitesses.


# Concepts avanc√©s (Bonus)

## Interpolation lin√©aire (lerp)
L'interpolation permet de cr√©er des transitions fluides entre deux vecteurs.

```java
PVector debut = new PVector(100, 100);
PVector fin = new PVector(400, 300);
float progression = 0; // De 0 √† 1

void draw() {
  background(240);
  
  // Progression automatique
  progression += 0.01;
  if (progression > 1) progression = 0;
  
  // Interpolation entre d√©but et fin
  PVector position = PVector.lerp(debut, fin, progression);
  
  ellipse(position.x, position.y, 20, 20);
}
```

## Distance entre deux points
```java
PVector point1 = new PVector(100, 100);
PVector point2 = new PVector(mouseX, mouseY);

float distance = PVector.dist(point1, point2);
// Ou : float distance = PVector.sub(point2, point1).mag();
```

## Rotation d'un vecteur
```java
PVector v = new PVector(50, 0); // Vecteur horizontal
v.rotate(PI/4); // Rotation de 45 degr√©s (PI/4 radians)
```

## Angle entre deux vecteurs
```java
PVector v1 = new PVector(1, 0);
PVector v2 = new PVector(0, 1);
float angle = PVector.angleBetween(v1, v2); // R√©sultat : PI/2 (90¬∞)
```

## Applications pratiques
- **Jeux de tir** : Calculer la trajectoire des projectiles
- **Animation** : Mouvements fluides et naturels
- **Intelligence artificielle** : Comportements de groupe (boids)
- **Physique** : Simulation de forces, collisions
- **Interface utilisateur** : Animations d'√©l√©ments GUI

---

# R√©f√©rences
- √Ä lire pour le prochain cours
- https://natureofcode.com/book/introduction/
- https://natureofcode.com/book/chapter-1-vectors/

## Ressources suppl√©mentaires
- [Documentation officielle PVector](https://processing.org/reference/PVector.html)
- [The Nature of Code - Vectors (vid√©o)](https://www.youtube.com/watch?v=mWJkvxQXIa8)
- [Exemples interactifs de vecteurs](https://p5js.org/examples/math-vector-math.html)